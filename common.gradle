apply plugin: "maven-publish"
apply plugin: 'signing'

project.group = rootProject.group
project.version = rootProject.version
project.description = "Transform Android project classes with Byte Buddy at compile time."
ext {
    publicName = "Android Buddy"
    url = "https://github.com/LikeTheSalad/android-buddy"
    vcsUrl = "https://github.com/LikeTheSalad/android-buddy.git"
    issueTrackerUrl = "https://github.com/LikeTheSalad/android-buddy/issues"
    tags = ['android', 'gradle', 'plugin', 'byte-buddy', 'android-buddy']
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            version = project.version

            artifact sourcesJar
            artifact javadocJar
            from components.java

            pom {
                name = project.publicName
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'LikeTheSalad'
                        name = 'Cesar Munoz'
                        email = 'likethesalad@gmail.com'
                    }
                }
                scm {
                    url = project.url
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.hasProperty('sonatypeUser') ? project.property('sonatypeUser') : System.getenv('SONATYPE_USER')
                password project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}